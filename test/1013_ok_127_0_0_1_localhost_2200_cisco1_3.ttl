file_name = "1013_ok_127_0_0_1_localhost_2200_cisco1_3.ttl"
target_display = "SSH踏み台"
target_ip = "127.0.0.1"
target_type = 3  ; 1=ping , 2=traceroute , 3=show run
next_ssh = 2  ; 0=Windows , 1=SSH login , 2=SSH->SSH login
base_type = 2  ; 1=cisco , 2=linux , 3=qx-s
base_display = "SSH接続先IP"
base_ip = "localhost:2200"
base_account = "admin"
next_type = 1  ; 1=cisco , 2=linux , 3=qx-s
next_display = "次のdisplay"
next_ip = "cisco1"
next_account = "nextadmin"



;INPUT_START

;file_name = "029c1_ok_connect.ttl"
;target_display = "pingを打つ装置"
;target_ip = "127.0.0.1"
;target_type = 1  ; 1=ping, 2=traceroute, 3=show run
;base_type = 2 ; 1=cisco, 2=linux, 3=qx-s
;base_display = "SSH接続する装置"
;base_ip = "localhost:2200"
;base_account = "foo" ; アカウント情報
;next_ssh = 2  ; "0=Windows, 1=SSHログイン", "2=SSH踏み台"
;next_type = 1 ; 1=cisco, 2=linux, 3=qx-s
;next_display = "SSHからさらに接続する装置"
;next_ip  = "c1"  ; 踏み台SSHのIPアドレス
;next_account = "bar"  ; 踏み台先のIPアドレス

;INPUT_END
ifdefined file_name
if result == 0 then
    file_name = "build/connect_test.ttl"
endif

ifdefined target_display
if result == 0 then
    target_display = "pingを打つ装置"
endif
ifdefined target_ip
if result == 0 then
    target_ip = "127.0.0.1"
endif
ifdefined target_type
if result == 0 then
    target_type = 1  ; 1=ping, 2=traceroute, 3=show run
endif
ifdefined base_type
if result == 0 then
    base_type = 2 ; 1=cisco, 2=linux, 3=qx-s
endif
ifdefined base_display
if result == 0 then
    base_display = "SSH接続する装置"
endif
ifdefined base_ip
if result == 0 then
    base_ip = "localhost:2200"
endif
ifdefined base_account
if result == 0 then
    base_account = "foo"
endif
ifdefined next_ssh
if result == 0 then
    next_ssh = 2
endif
ifdefined next_type
if result == 0 then
    next_type = 1 ; 1=cisco, 2=linux, 3=qx-s
endif
ifdefined next_display
if result == 0 then
    next_display = "SSHからさらに接続する装置"
endif
ifdefined next_ip
if result == 0 then
    next_ip  = "c1"  ; 踏み台SSHのIPアドレス
endif
ifdefined next_account
if result == 0 then
    next_account = "bar"  ; 踏み台先のIPアドレス
endif

; ログファイルを設定する
log_file_name_work = file_name
strreplace log_file_name_work 1 'ttl' ''
strconcat log_file_name_work 'log'  ; 拡張子がttlでないときは replaceでlogできないので追加
; ログファイルを絶対パスに変える
getdir log_file_name
strconcat log_file_name #92
strconcat log_file_name log_file_name_work

; ログを開く
logopen log_file_name 0 0

;; パスワード保存ファイルを指定する
getdir key_file
strconcat key_file #92 ;
strconcat key_file "pass" ; パスワード保存用


; すでに接続されていたらエラー
testlink
if result==2  then
    messagebox "already connected" "title"
    call call_ending
    end
endif

;; ベースとなるパスワードを取得する
call call_base_password
base_password = password

; 接続する 'localhost:2200 /ssh /auth=password /user=aaa /passwd=bbb'
command = base_ip
strconcat command " /ssh /auth=password /user="
strconcat command base_account
strconcat command " /passwd="
strconcat command base_password
connect command
if result <> 2 then
    int2str strvar result
    message = "connection failer"
    strconcat message strvar
    messagebox message "title"
    call call_ending
    end
endif


prompt = '$'
timeout = 10
command = 'pwd'
sendln command
wait command
if result == 0 then
    messagebox 'input check timeout failer' 'title'
    call call_ending
    end
endif
while 1
    ; プロンプトチェック
    wait '>' '#' '$' '(yes/no)' 'assword:' '-- More --' 
    result_type = result
    if result_type == 0 then
        messagebox 'prompt check timeout failer' 'title'
        call call_ending
        end
    elseif result_type == 1 then
        prompt = '>'
        if base_type == 1 then
            ; ciscoの場合は特権へ移行
            sendln 'enable'
            continue
        endif
    elseif result_type == 2 then
        prompt = '#'
    elseif result_type == 3 then
        prompt = '$'
    elseif result_type == 4 then
        sendln "yes"
        continue
    elseif result_type == 5 then
        strcompare prompt '>'
        if result == 0 then
            call call_base_password_enable  ; 特権モード移行用
        else
            call call_base_password
        endif
        sendln password
        continue
    elseif result_type == 6 then
        sendln ''
        continue
    else
        int2str strvar result_type
        message = "prompt not found("
        strconcat message strvar
        strconcat message ")"
        messagebox message 'title'
        call call_ending
        end
    endif
    ;
    ; while を終わる
    break
endwhile


if next_ssh == 2 then
    ; ssh 接続 'ssh account@ip'
    command = "ssh "
    strconcat command next_account
    strconcat command "@"
    strconcat command next_ip
    sendln command
    while 1
        ; プロンプトチェック
        wait '>' '#' '$' '(yes/no)' 'assword:' '-- More --'
        result_type = result
        if result_type == 0 then
            messagebox 'next prompt check fail!' 'title'
            call call_ending
            end
        elseif result_type == 1 then
            prompt = '>'
            if base_type == 1 then
                sendln 'enable'
                continue
            endif
        elseif result_type == 2 then
            prompt = '#'
        elseif result_type == 3 then
            prompt = '$'
        elseif result_type == 4 then
            sendln "yes"
            continue
        elseif result_type == 5 then
            strcompare prompt '>'
            if result == 0 then
                call call_next_enable_password ; 特権モード移行用
            else
                call call_next_password
            endif
            sendln password
            continue
        elseif result_type == 6 then
            sendln ''
            continue
        else
            int2str strvar result_type
            message = "next promot not found("
            strconcat message strvar
            strconcat message ")"
            messagebox message 'title'
            call call_ending
            end
        endif
        ;
        ; while を終わる
        break
    endwhile
endif

; サーバ種別を決める
if next_ssh == 0 then
    server_type = base_type
else
    server_type = next_type
endif


;; ここでおまじない必要
if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
    command = 'terminal length 0'
    sendln command
    wait command
    wait prompt
elseif server_type == 3 then ; 1=cisco, 2=linux, 3=qx-s
    command = 'screen-length disable'
    sendln command
    wait command
    wait prompt
    command = 'terminal pager offscreen-length disable'
    sendln command
    wait command
    wait prompt
    command = 'set terminal pager disable'
    sendln command
    wait command
    wait prompt
endif

;; 実コマンドの投入
if target_type = 1 then  ; 1=ping, 2=traceroute, 3=show run
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        command = 'ping '
    elseif server_type == 2 then
        command = 'ping -c 1 '
    else
        command = 'ping '
    endif
    strconcat command target_ip
    sendln command
    wait command
    wait prompt '0% packet loss' 'Success rate is 100 percent'
    if result <= 1 then
        assert 1 "OK 222 xx" result prompt
        call call_ending
        end
    endif
elseif target_type = 2 then 
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        command = 'traceroute '
    elseif server_type == 2 then
        command = 'tracepath '
    else
        command = 'traceroute '
    endif
    strconcat command target_ip
    sendln command
    wait prompt
    if result == 0 then
        messagebox 'command is time up!' command
        call call_ending
        end
    endif
else
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        command = 'show running-config'
    elseif server_type == 2 then
        command = 'ifconfig -a'
    else
        command = 'display current-configuration'
    endif
    sendln command
    ; プロントのチェックを行う
    wait prompt
    if result == 0 then
        messagebox 'command is time up!' command
        call call_ending
        end
    endif
endif

; 終了処理
call call_ending

; 正常終了を通知
error=''
result = 1
assert 1 result
end

; SSHでログインするときに使うパスワード
:call_base_password
key_target = 'normal_'
strconcat key_target base_account
key_ip = base_ip
key_base_display = base_display
call call_pass_all
return

; SSHからSSHにさらにログインするときのパスワード
:call_base_password_enable
key_target = 'enable_'
strconcat key_target base_account
key_ip = base_ip
key_base_display = base_display
call call_pass_all
return

; 特権ユーザのパスワード
:call_next_password
key_target = 'normal_'
strconcat key_target next_account
key_ip = next_ip
key_base_display = next_display
call call_pass_all
return

; 特権ユーザのパスワード
:call_next_enable_password
key_target = 'enable_'
strconcat key_target next_account
key_ip = next_ip
key_base_display = next_display
return

; パスワード関連まとめ
:call_pass_all
key_ip_replace = key_ip
strreplace key_ip_replace 1 ':' '_'
key_data = key_file
strconcat key_data '_'
strconcat key_data key_ip_replace
strconcat key_data '.key'
ispassword key_data key_target  ; パスワードの有無確認
if result == 0 then  ; 設定されていない
    message = key_target
    strconcat message "を入力"
    strconcat message #13
    strconcat message key_ip
    passwordbox "enableパスを入力" base_display
    password = inputstr ; パスワードを入力
    setpassword key_data key_target password  ; パスワードを設定
else
    getpassword key_data key_target password  ; パスワードを取得
endif
return

:call_ending
testlink
if result==2  then
    closett
endif
logclose
result = 0
return
