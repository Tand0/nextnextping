
;INPUT_START

;file_name = "029c1_ok_connect.ttl"
;target_display = "pingを打つ装置"
;target_ip = "127.0.0.1"
;target_type = 1  ; 1=ping, 2=traceroute, 3=show run
;base_display = "SSH接続する装置"
;base_ip = "localhost:2200"
;base_account = "foo" ; アカウント情報
;next_ssh = 2  ; "0=Windows", "1=SSHログイン", "2=SSH踏み台"
;next_display = "SSHからさらに接続する装置"
;next_ip  = "c1"  ; 踏み台SSHのIPアドレス
;next_account = "bar"  ; 踏み台先のIPアドレス

;INPUT_END
ifdefined file_name
if result == 0 then
    file_name = "build/connect_test.ttl"
endif

ifdefined target_display
if result == 0 then
    target_display = "pingを打つ装置"
endif
ifdefined target_ip
if result == 0 then
    target_ip = "127.0.0.1"
endif
ifdefined target_type
if result == 0 then
    target_type = 1  ; 1=ping, 2=traceroute, 3=show run
endif
ifdefined base_display
if result == 0 then
    base_display = "SSH接続する装置"
endif
ifdefined base_ip
if result == 0 then
    base_ip = "localhost:2200"
endif
ifdefined base_account
if result == 0 then
    base_account = "foo"
endif
ifdefined next_ssh
if result == 0 then
    next_ssh = 2
endif
ifdefined next_display
if result == 0 then
    next_display = "SSHからさらに接続する装置"
endif
ifdefined next_ip
if result == 0 then
    next_ip  = "c1"  ; 踏み台SSHのIPアドレス
endif
ifdefined next_account
if result == 0 then
    next_account = "bar"  ; 踏み台先のIPアドレス
endif

; ログファイルを設定する
log_file_name_work = file_name
strreplace log_file_name_work 1 'ttl' ''
strconcat log_file_name_work 'log'  ; 拡張子がttlでないときは replaceでlogできないので追加
; ログファイルを絶対パスに変える
getdir log_file_name
strconcat log_file_name #92
strconcat log_file_name log_file_name_work

; ログを開く
logopen log_file_name 0 0

; すでに接続されていたらエラー
testlink
if result==2  then
    messagebox "already connected" "title"
    call call_ending
    end
endif

;; ベースとなるパスワードを取得する
call call_base_password
base_password = password

; 接続する 'localhost:2200 /ssh /auth=password /user=aaa /passwd=bbb'
command = base_ip
strconcat command " /ssh /auth=password /user="
strconcat command base_account
strconcat command " /passwd="
strconcat command base_password
connect command
if result <> 2 then
    int2str strvar result
    message = "connection failer "
    strconcat message strvar
    messagebox command message
    call call_ending
    end
endif

; ttl本家側で入れないと動かないことがあった
pause 1

;
prompt = '$'
timeout = 10
state_flag = 0  ; call_get_promptのために状態フラグを設定する
command = 'clear'
call call_get_prompt  ; プロンプトを決定する

if next_ssh == 2 then
    state_flag = 1  ; call_get_promptのために状態フラグを設定する
    ; ssh 接続 'ssh account@ip'
    command = "ssh "
    strconcat command next_account
    strconcat command "@"
    strconcat command next_ip
    call call_get_prompt  ; プロンプトを決定する
endif


if server_type == 1 then  ; 1=cisco, 2=linux, 3=qx-s
    ;; cisco のときはこれで -- More -- を出さないようにする
    command = 'terminal length 0'
    call call_one_command
elseif server_type == 3 then  ; 1=cisco, 2=linux, 3=qx-s
    ;; qx-s のときはこれで -- More -- を出さないようにする
    command = 'screen-length disable'
    call call_one_command
    command = 'terminal pager offscreen-length disable'
    call call_one_command
    command = 'set terminal pager disable'
    call call_one_command
endif

; 特殊コマンドを実行する
my_command_flag = 1
ifdefined my_command_int
if result<>1 then
    my_command_flag = 0
endif
ifdefined my_command
if result<>6 then
    my_command_flag = 0
endif

;; 実コマンドの投入
if my_command_flag<>0 then
    for i   0   (my_command_int - 1)
        command = my_command[i]
        strcompare command ""
        if result<>0 then
            call call_check_command
            if result <> 0 then
                call call_get_prompt
            else
                call call_one_command
            endif
        endif
    next
elseif target_type = 1 then  ; 1=ping, 2=traceroute, 3=show run
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        command = 'ping '
    elseif server_type == 2 then
        command = 'ping -c 1 '
    else
        command = 'ping '
    endif
    strconcat command target_ip
    flushrecv
    sendln command
    wait command
    wait prompt ' 0% packet loss' ' 0.0% packet loss' 'Success rate is 100 percent'
    if result <= 1 then
        call call_ending
        end
    endif
elseif target_type = 2 then 
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        command = 'traceroute '
        strconcat command target_ip
        call call_one_command
    elseif server_type == 2 then
        strcompare "] " prompt
        if result == 0 then
            ; for vmware
            command = 'traceroute '
            strconcat command target_ip
            call call_one_command
        else
            command = 'tracepath '
            strconcat command target_ip
            call call_one_command
        endif
    else
        command = 'traceroute '
        strconcat command target_ip
        call call_one_command
    endif
else
    if server_type == 1 then ; 1=cisco, 2=linux, 3=qx-s
        ; for cisco
        command = 'show running-config'
        call call_one_command
        command = 'show ip interface brief'
        call call_one_command
    elseif server_type == 2 then
        strcompare "] " prompt
        if result == 0 then
            ; for vmware
            command = 'esxcli network nic list'
            call call_one_command
            command = 'esxcli network vswitch standard list'
            call call_one_command
            command = 'esxcli network ip interface list'
            call call_one_command
        else
            ; for linux
            command = 'ifconfig -a'
            call call_one_command
            command = 'ip addr show'
            call call_one_command
            command = 'ip link show'
            call call_one_command
            command = 'ip route show'
            call call_one_command
            command = 'ss'
            call call_one_command
        endif
    else
        ; for qx-s
        command = 'display current-configuration'
        call call_one_command
    endif
endif

; 終了処理
call call_ending

; 正常終了を通知
error=''
result = 1
end


; SSHでログインするときに使うパスワード
:call_base_password
    key_target = 'normal_'
    strconcat key_target base_account
    key_ip = base_ip
    key_base_display = base_display
    call call_pass_all
return

; SSHからSSHにさらにログインするときのパスワード
:call_base_password_enable
    key_target = 'enable_'
    strconcat key_target base_account
    key_ip = base_ip
    key_base_display = base_display
    call call_pass_all
return

; 特権ユーザのパスワード
:call_next_password
    key_target = 'normal_'
    strconcat key_target next_account
    key_ip = next_ip
    key_base_display = next_display
    call call_pass_all
return

; 特権ユーザのパスワード
:call_next_enable_password
    key_target = 'enable_'
    strconcat key_target next_account
    key_ip = next_ip
    key_base_display = next_display
    call call_pass_all
return

; パスワード関連まとめ
:call_pass_all
    key_ip_replace = key_ip
    strreplace key_ip_replace 1 ':' '_' ; ipv6
    strreplace key_ip_replace 1 #92'.' '_'  ; ipv4 正規表現なので単純に.を渡すと全部消える
    getdir key_data
    strconcat key_data #92
    strconcat key_data "pass_" ; パスワード保存用
    strconcat key_data key_ip_replace
    strconcat key_data '.key'
    ispassword key_data key_target  ; パスワードの有無確認
    if result == 0 then  ; 設定されていない
        message = key_target
        strconcat message "("
        strconcat message key_ip
        strconcat message ")"
        passwordbox message base_display
        password = inputstr ; パスワードを入力
        setpassword key_data key_target password  ; パスワードを設定
    else
        getpassword key_data key_target password  ; パスワードを取得
    endif
    return

    :call_ending
    testlink
    if result==2  then
        closett
        ; logclose
    endif
    result = 0
return


:call_check_command
    ; コマンドがプロンプトの変化を要求しているか確認する
    strscan command "clear"
    if result<>0 then
        return
    endif
    strscan command "ssh "
    if result<>0 then
        return
    endif
    strscan command "quit"
    if result<>0 then
        return
    endif
    strscan command "exit"
    if result<>0 then
        return
    endif
    strscan command "system-view"
return


:call_one_command
    ; １コマンド分の処理
    flushrecv
    sendln command
    wait command
    if result == 0 then
        messagebox 'input command timeout failer' command
        call call_ending
        end
    endif
    ; プロントのチェックを行う
    while 1
        ;
        wait prompt '-- More --'
        if result = 0 then
            messagebox 'command is time up!' command
            call call_ending
            end
        elseif result = 2 then
            flushrecv
            sendln ''
            continue
        endif
        ;
        break
    endwhile
return

 :call_get_prompt
    flushrecv
    sendln command
    wait command
    if result == 0 then
        messagebox 'input command timeout failer' command
        call call_ending
        end
    endif
    password_flag = 0
    enable_flag = 0
    while 1
        ; プロンプトチェック
        wait '>' '#' '$' '] ' '(yes/no)' 'assword:' '-- More --'
        result_type = result
        if result_type == 0 then
            ; タイムアウトのとき
            messagebox 'next prompt check fail!' 'title'
            call call_ending
            end
        elseif result_type == 1 then
            prompt = '>'
            if enable_flag == 0 then
                ; '>'が来たら一度だけenableを打ち、2度来たらqxモードと見なす
                server_type = 1  ; 1=cisco, 2=linux, 3=qx-s
                password_flag = 0
                enable_flag = 1
                command = 'enable'
                flushrecv
                sendln command
                wait command
                continue
            else
                server_type = 3  ; 1=cisco, 2=linux, 3=qx-s
            endif
        elseif result_type == 2 then
            server_type = 1  ; 1=cisco, 2=linux, 3=qx-s
            prompt = '#'
        elseif result_type == 3 then
            server_type = 2  ; 1=cisco, 2=linux, 3=qx-s
            prompt = '$'
        elseif result_type == 4 then
            server_type = 2  ; 1=cisco, 2=linux, 3=qx-s
            prompt = '] '  ; for vmware
        elseif result_type == 5 then
            command = 'yes'
            flushrecv
            sendln command
            wait command
            continue
        elseif result_type == 6 then
            if password_flag <> 0 then
                message = 'passdword ng!'
                messagebox message 'title'
                call call_ending
                end
            endif
            password_flag = 1
            if state_flag == 0 then
                if enable_flag<>0 then
                    call call_base_password_enable  ; 特権モード移行用
                else
                    call call_base_password
                endif
            else
                if enable_flag<>0 then
                    call call_next_enable_password  ; 特権モード移行用
                else
                    call call_next_password
                endif
            endif
            flushrecv
            sendln password
            continue
        elseif result_type == 7 then
            flushrecv
            sendln ''
            continue
        else
            int2str strvar result_type
            message = "next promot not found("
            strconcat message strvar
            strconcat message ")"
            messagebox message 'title'
            call call_ending
            end
        endif
        ;
        ; while を終わる
        break
    endwhile
return
